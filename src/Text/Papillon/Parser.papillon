{-# LANGUAGE FlexibleContexts, TemplateHaskell, UndecidableInstances #-}

module Text.Papillon.Parser (
	Peg,
	Definition(..),
	Selection(..),
	ExpressionHs(..),
	NameLeaf(..),
	NameLeaf_(..),
	ReadFrom(..),

	getSelectionType,
	showNameLeaf,
	nameFromRF,

	parse,
	Source(..),
	SourceList(..),
	Derivs(pegFile, peg, char),
	ParseError(..),
	mkParseError,
	pePositionS,
	Pos(..),
	ListPos(..),

	PPragma(..),
	ModuleName,
	ExportList,
	Code
) where

import Text.Papillon.SyntaxTree
import Language.Haskell.TH (
	Name, TypeQ, PatQ, ExpQ, mkName,
	conT, listT, appT, arrowT, wildP, litP, conP, tupP, listP, uInfixP,
	litE, varE, conE, tupE, listE, sigE, appE, infixE, uInfixE, lamE,
	integerL, charL, stringL)
import Data.Char (isSpace, isDigit, isUpper, isLower)

[papillon|

pegFile :: PegFile
	= pr:pragmas md:moduleDec pip:preImpPap _:importPapillon pp:prePeg _:pap
		p:peg _:spaces '|' ']' '\n' atp:afterPeg
			{ mkPegFile pr md pip pp p atp }
	/ pr:pragmas md:moduleDec pp:prePeg _:pap p:peg _:spaces
		'|' ']' '\n' atp:afterPeg
			{ mkPegFile pr md [] pp p atp }
;
pragmas :: [PPragma]
	= _:spaces pr:pragma prs:pragmas	{ pr : prs }
	/ _:spaces				{ [] }
;
pragma :: PPragma
	= '{' '-' '#' _:spaces 'L' 'A' 'N' 'G' 'U' 'A' 'G' 'E' _:spaces
		s:pragmaItems _:pragmaEnd _:spaces	{ LanguagePragma s }
	/ '{' '-' '#' _:spaces s:pragmaStr _:pragmaEnd	{ OtherPragma s }
;
pragmaStr :: String
	= !_:pragmaEnd c s:pragmaStr		{ c : s }
	/					{ "" }
;
pragmaItems :: [String]
	= t:typToken ',' _:spaces i:pragmaItems	{ t : i }
	/ t:typToken				{ [t] }
;
pragmaEnd :: ()
	= _:spaces '#' '-' '}'			{ () }
;
moduleDec :: Maybe ([String], Maybe String)
	= 'm' 'o' 'd' 'u' 'l' 'e' _:spaces
		n:moduleName _:spaces
		'(' _:spaces s:moduleDecStr _:whr
						{ Just (n, Just s) }
	/ 'm' 'o' 'd' 'u' 'l' 'e' _:spaces
		n:moduleName _:spaces 'w' 'h' 'e' 'r' 'e' _:spaces
						{ Just (n, Nothing) }
	/					{ Nothing }
;
moduleName :: ModuleName
	= t:typ '.' n:moduleName		{ t : n }
	/ t:typ					{ [t] }
;
moduleDecStr :: String
	= !_:whr c s:moduleDecStr		{ c : s }
	/					{ "" }
;
whr :: ()
	= _:spaces ')' _:spaces 'w' 'h' 'e' 'r' 'e'
						{ () }
;
preImpPap :: String
	= !_:importPapillon !_:pap c pip:preImpPap
						{ c : pip }
	/					{ "" }
;
prePeg :: String
	= !_:pap c pp:prePeg			{ c : pp }
	/					{ "" }
;
afterPeg :: String
	= c atp:afterPeg			{ c : atp }
	/					{ "" }
;
importPapillon :: ()
	= "import":varToken "Text":typToken '.' _:spaces "Papillon":typToken
		!'.'				{ () }
;
varToken :: String
	= v:variable _:spaces			{ v }
;
typToken :: String
	= t:typ _:spaces			{ t }
;
pap :: ()
	= '\n' '[' 'p' 'a' 'p' 'i' 'l' 'l' 'o' 'n' '|' '\n'	{ () }
;
peg :: TTPeg
	= _:spaces s:sourceType p:peg_		{ mkTTPeg s p }
	/ p:peg_				{ mkTTPeg "String" p }
;
sourceType :: String
	= "source":varToken ':' _:spaces v:typToken
						{ v }
;
peg_ :: Peg
	= _:spaces d:definition p:peg_		{ d : p }
	/ 					{ [] }
;
definition :: Definition
	= v:variable _:spaces ':' ':'
		_:spaces t:hsTypeArr _:spaces '='
		_:spaces sel:selection _:spaces ';'
						{ Definition v t sel }
	/ v:variable _:spaces '<' '-' _:spaces
		sel:plainSelection _:spaces ';'
				{ PlainDefinition v $ PlainSelection sel }
;
selection :: Selection
	= s:normalSelection			{ Selection s }
	/ s:plainSelection			{ PlainSelection s }
;
normalSelection :: [ExpressionHs]
	= ex:expressionHs _:spaces '/' _:spaces sel:normalSelection
						{ ex : sel }
	/ ex:expressionHs			{ [ex] }
;
plainSelection :: [ExpressionHs]
	= ex:plainExpressionHs _:spaces '/' _:spaces sel:plainSelection
						{ ex : sel }
	/ ex:plainExpressionHs			{ [ex] }
;
expressionHs :: ExpressionHs
	= e:expression _:spaces '{' _:spaces h:hsExpLam _:spaces '}'
						{ ExpressionHs e h }
	/ e:expressionHsSugar			{ e }
;
expressionHsSugar :: ExpressionHs
	= '<' _:spaces h:hsExpLam _:spaces '>'	{ ExpressionHsSugar h }
;
plainExpressionHs :: ExpressionHs
	= rfs:(rf:plainReadFromLs _:spaces { rf })*
						{ PlainExpressionHs rfs }
;
plainReadFromLs :: ReadFrom
	= rf:readFromLs				{ rf }
	/ rf:selectCharsLs			{ rf }
;
expression :: Expression
	= l:nameLeaf_ _:spaces e:expression	{ l : e }
	/ 					{ [] }
;
nameLeaf_ :: NameLeaf_
	= '!' nl:nameLeafNoCom _:spaces com:comForErr?
						{ NotAfter nl $ maybe "" id com }
	/ '&' nl:nameLeaf			{ After nl }
	/ nl:nameLeaf				{ Here nl }
;
nameLeaf :: NameLeaf
	= n:pat1 _:spaces com:comForErr? ':' (rf, p):leaf
		{ NameLeaf (n, maybe "" id com) rf p }
	/ n:pat1 _:spaces com:comForErr?
		{ NameLeaf (n, maybe "" id com) FromToken Nothing }
;
nameLeafNoCom :: NameLeaf
	= n:pat1 _:spaces com:comForErr? ':' (rf, p):leaf
		{ NameLeaf (n, maybe "" id com) rf p }
	/ n:pat1 _:spaces
		{ NameLeaf (n, "") FromToken Nothing }
;
comForErr :: String
	= '{' '-' '#' _:spaces '"' s:stringLit '"' _:spaces '#' '-' '}' _:spaces
						{ s }
;
leaf :: (ReadFrom, Maybe (ExpQ, String))
	= rf:readFromLs t:test			{ (rf, Just t) }
	/ rf:readFromLs				{ (rf, Nothing) }
	/ t:test				{ (FromToken, Just t) }
;
patOp :: PatQ
	= p:pat o:opConName po:patOp		{ uInfixP p o po }
	/ p:pat _:spaces '`' t:typ '`' _:spaces po:patOp
						{ uInfixP p (mkName t) po }
	/ p:pat					{ p }
;
pat :: PatQ
	= t:typ _:spaces ps:pats		{ conToPatQ t ps }
	/ '(' o:opConName ')'_:spaces ps:pats	{ conP o ps }
	/ p:pat1				{ p }
;
pat1 :: PatQ
	= t:typ					{ conToPatQ t [] }
	/ "_":variable				{ wildP }
	/ n:variable				{ strToPatQ n }
	/ i:integer				{ litP (integerL i) }
	/ '-' _:spaces i:integer		{ litP (integerL $ - i) }
	/ '\'' c:charLit '\''			{ litP $ charL c }
	/ '"' s:stringLit '"'			{ litP $ stringL s }
	/ '(' p:patList ')'			{ tupP p }
	/ '[' p:patList ']'			{ listP p }
;
patList :: [PatQ]
	= p:patOp _:spaces ',' _:spaces ps:patList
						{ p : ps }
	/ p:patOp				{ [p] }
	/					{ [] }
;
opConName :: Name
	= ':' ot:opTail				{ mkName $ ':' : ot }
;
charLit :: Char
	= c:[isAlphaNumOt c]			{ c }
	/ '\\' c:escapeC			{ c }
;
stringLit :: String
	= c:[isStrLitC c] s:stringLit		{ c : s }
	/ '\\' c:escapeC s:stringLit		{ c : s }
	/					{ "" }
;
escapeC :: Char
	= '"'					{ '"' }
	/ '\''					{ '\'' }
	/ '\\'					{ '\\' }
	/ 'n'					{ '\n' }
	/ 't'					{ '\t' }
;
pats :: PatQs
	= p:pat _:spaces ps:pats		{ p : ps }
	/					{ [] }
;
readFromLs :: ReadFrom
	= rf:readFrom '*'			{ FromList rf }
	/ rf:readFrom '+'			{ FromList1 rf }
	/ rf:readFrom '?'			{ FromOptional rf }
	/ rf:readFrom				{ rf }
;
readFrom :: ReadFrom
	= v:variable				{ FromVariable v }
	/ '(' s:selection ')'			{ FromSelection s }
	/ e:expressionHsSugar			{ FromSelection $ Selection [e] }
;
selectCharsLs :: ReadFrom
	= rf:selectChars '*'			{ FromList rf }
	/ rf:selectChars '+'			{ FromList1 rf }
	/ rf:selectChars '?'			{ FromOptional rf }
	/ rf:selectChars			{ rf }
;
selectChars :: ReadFrom
	= '[' cs:(c:[isLower c] { c })+ ']'	{ FromTokenChars cs }
	/ '[' cb:[cb `notElem` "\\-"] '-'
		ce:[ce `notElem` "\\-"] ']'	{ FromTokenChars [cb .. ce] }
	/ '\'' c:[c `notElem` "\\'"] '\''	{ FromTokenChars [c] }
;
test :: (ExR, String)
	= '[' h:hsExpLam _:spaces com:comForErr? ']'
						{ (h, maybe "" id com) }
;
hsExpLam :: ExR
	= '\\' _:spaces ps:pats _:spaces '-' '>' _:spaces e:hsExpTyp
						{ lamE ps e }
	/ e:hsExpTyp				{ e }
;
hsExpTyp :: ExR
	= eo:hsExpOp ':' ':' _:spaces t:hsTypeArr
						{ sigE eo t }
	/ eo:hsExpOp				{ eo }
;
hsExpOp :: ExR
	= l:hsExp _:spaces o:hsOp _:spaces r:hsExpOp
						{ uInfixE (getEx l) o r }
	/ e:hsExp				{ getEx e }
;
hsOp :: ExR
	= c:[isOpHeadChar c] o:opTail 		{ varE $ mkName $ c : o }
	/ ':' !':' o:opTail			{ conE $ mkName $ ':' : o }
	/ '`' v:variable '`'			{ varE $ mkName v }
	/ '`' t:typ '`'				{ conE $ mkName t }
;
opTail :: String
	= c:[isOpTailChar c] s:opTail		{ c : s }
	/					{ "" }
;
hsExp :: Ex
	= e:hsExp1 _:spaces h:hsExp		{ applyExR e h }
	/ e:hsExp1				{ toEx e }
;
hsExp1 :: ExR
	= '(' l:(e:hsExpTyp {e})? _:spaces o:hsOp _:spaces r:(e:hsExpTyp {e})? ')'
						{ infixE l o r }
	/ '(' et:hsExpTpl ')'			{ tupE et }
	/ '[' et:hsExpTpl ']'			{ listE et }
	/ v:variable				{ varE $ mkName v }
	/ t:typ					{ conE $ mkName t }
	/ i:integer _:spaces			{ litE $ integerL i }
	/ '\'' c:charLit '\''			{ litE $ charL c }
	/ '"' s:stringLit '"'			{ litE $ stringL s }
	/ '-' _:spaces e:hsExp1			{ appE (varE $ mkName "negate") e }
;
hsExpTpl :: ExRL
	= e:hsExpLam _:spaces ',' _:spaces et:hsExpTpl
						{ e : et }
	/ e:hsExpLam				{ [e] }
	/					{ [] }
;
hsTypeArr :: TypeQ
	= l:hsType '-' '>' _:spaces r:hsTypeArr
						{ appT (appT arrowT $ l id) r }
	/ t:hsType				{ t id }
;
hsType :: (TypeQ -> TypeQ) -> TypeQ
	= t:hsType1 ts:hsType			{ applyTyp ($ t) ts }
	/ t:hsType1				{ ($ t) }
;
hsType1 :: TypeQ
	= '[' ']' _:spaces			{ listT }
	/ '[' t:hsTypeArr ']' _:spaces		{ listT `appT` t }
	/ '(' _:spaces tt:hsTypeTpl ')' _:spaces{ tupT tt }
	/ t:typToken				{ conT $ mkName t }
	/ '(' '-' '>' ')' _:spaces		{ arrowT }
;
hsTypeTpl :: TypeQL
	= t:hsTypeArr ',' _:spaces tt:hsTypeTpl
						{ t : tt }
	/ t:hsTypeArr				{ [t] }
	/					{ [] }
;
typ :: String
	= u:upper t:tvtail			{ u : t }
;
variable :: String
	= l:lower t:tvtail			{ l : t }
;
tvtail :: String
	= a:alpha t:tvtail			{ a : t }
	/ 					{ "" }
;
integer :: Integer
	= dh:digit ds:(d:digit { d })*		{ read $ dh : ds }
;
alpha :: Char
	= u:upper				{ u }
	/ l:lower				{ l }
	/ d:digit				{ d }
	/ '\''					{ '\'' }
;
upper :: Char
	= u:[isUpper u]				{ u }
;
lower :: Char
	= l:[isLower l || l == '_' ]		{ l }
;
digit :: Char
	= d:[isDigit d]				{ d }
;
spaces :: ()
	= _:space _:spaces			{ () }
	/ 					{ () }
;
space :: ()
	= s:[isSpace s]				{ () }
	/ '-' '-' _:notNLString _:newLine	{ () }
	/ _:comment				{ () }
;
notNLString :: String
	= !_:newLine c s:notNLString		{ c : s }
	/					{ "" }
;
newLine :: ()
	= '\n'					{ () }
;
comment :: ()
	= '{' '-' !'#' _:comments _:comEnd	{ () }
;
comments :: ()
	= _:notComStr _:comment _:comments	{ () }
	/ _:notComStr				{ () }
;
notComStr :: ()
	= !_:comment !_:comEnd _ _:notComStr	{ () }
	/					{ () }
;
comEnd :: ()
	= '-' '}'				{ () }
;

|]
