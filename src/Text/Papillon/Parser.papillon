{-# LANGUAGE FlexibleContexts, TemplateHaskell, UndecidableInstances #-}

module Text.Papillon.Parser (
	Lookahead(..),
	Lists(..),

	Peg,
	Definition,
	Selection,
	Expression,
	PlainExpression,
	Check,
	ReadFrom(..),

	PegQ,
	DefinitionQ,
	SelectionQ,
	ExpressionQ,
	CheckQ,
	ReadFromQ,

	selectionType,
	showCheck,
	showCheckQ,
	nameFromRF,

	parse,
	Source(..),
	SourceList(..),
	Derivs(pegFile, peg, char),
	ParseError(..),
	mkParseError,
	pePositionS,
	Pos(..),
	ListPos(..),

	PegFile,
	PegFileQ,
	PPragma(..),
	ModuleName,
	Exports,
	Code
) where

import Text.Papillon.SyntaxTree
import Language.Haskell.TH (
	Name, TypeQ, PatQ, ExpQ, mkName,
	conT, tupleT, listT, appT, arrowT,
	wildP, litP, varP, conP, tupP, listP, uInfixP,
	litE, varE, conE, tupE, listE, sigE, appE, infixE, uInfixE, lamE,
	integerL, charL, stringL)
import Data.Char (isSpace, isDigit, isUpper, isLower)

[papillon|

pegFile :: PegFileQ
	= pr:pragmas md:moduleDec pip:preImpPap _:importPapillon pp:prePeg _:pap
		p:peg _:spaces '|' ']' '\n' atp:afterPeg
			{ mkPegFile pr md pip pp p atp }
	/ pr:pragmas md:moduleDec pp:prePeg _:pap p:peg _:spaces
		'|' ']' '\n' atp:afterPeg
			{ mkPegFile pr md [] pp p atp }
;
pragmas :: [PPragma]
	= _:spaces pr:pragma prs:pragmas	{ pr : prs }
	/ _:spaces				{ [] }
;
pragma :: PPragma
	= '{' '-' '#' _:spaces 'L' 'A' 'N' 'G' 'U' 'A' 'G' 'E' _:spaces
		s:pragmaItems _:pragmaEnd _:spaces	{ LanguagePragma s }
	/ '{' '-' '#' _:spaces s:pragmaStr _:pragmaEnd	{ OtherPragma s }
;
pragmaStr :: String
	= !_:pragmaEnd c s:pragmaStr		{ c : s }
	/					{ "" }
;
pragmaItems :: [String]
	= t:typToken ',' _:spaces i:pragmaItems	{ t : i }
	/ t:typToken				{ [t] }
;
pragmaEnd :: ()
	= _:spaces '#' '-' '}'			{ () }
;
moduleDec :: Maybe ([String], Maybe String)
	= 'm' 'o' 'd' 'u' 'l' 'e' _:spaces
		n:moduleName _:spaces
		'(' _:spaces s:moduleDecStr _:whr
						{ Just (n, Just s) }
	/ 'm' 'o' 'd' 'u' 'l' 'e' _:spaces
		n:moduleName _:spaces 'w' 'h' 'e' 'r' 'e' _:spaces
						{ Just (n, Nothing) }
	/					{ Nothing }
;
moduleName :: ModuleName
	= t:typ '.' n:moduleName		{ t : n }
	/ t:typ					{ [t] }
;
moduleDecStr :: String
	= !_:whr c s:moduleDecStr		{ c : s }
	/					{ "" }
;
whr :: ()
	= _:spaces ')' _:spaces 'w' 'h' 'e' 'r' 'e'
						{ () }
;
preImpPap :: String
	= !_:importPapillon !_:pap c pip:preImpPap
						{ c : pip }
	/					{ "" }
;
prePeg :: String
	= !_:pap c pp:prePeg			{ c : pp }
	/					{ "" }
;
afterPeg :: String
	= c atp:afterPeg			{ c : atp }
	/					{ "" }
;
importPapillon :: ()
	= "import":varToken "Text":typToken '.' _:spaces "Papillon":typToken
		!'.'				{ () }
;
varToken :: String
	= v:variable _:spaces			{ v }
;
typToken :: String
	= t:typ _:spaces			{ t }
;
pap :: ()
	= '\n' '[' 'p' 'a' 'p' 'i' 'l' 'l' 'o' 'n' '|' '\n'	{ () }
;
peg :: STPegQ
	= _:spaces s:sourceType p:peg_		{ stPegQ (conT $ mkName s) p }
	/ p:peg_				{ stPegQ (conT $ mkName "String") p }
;
sourceType :: String
	= "source":varToken ':' _:spaces v:typToken
						{ v }
;
peg_ :: PegQ
	= _:spaces d:definition p:peg_		{ \g -> (:) <$> d g <*> p g }
	/ 					{ const $ return [] }
;
definition :: DefinitionQ
	= v:variable _:spaces ':' ':'
		_:spaces t:hsTypeArr _:spaces '='
		_:spaces sel:selection _:spaces ';'
						{ definitionQ v (Just t) sel }
	/ v:variable _:spaces '<' '-' _:spaces
		sel:plainSelection _:spaces ';'	{ definitionQ v Nothing $
							plainSelectionQ sel }
;
selection :: SelectionQ
	= s:normalSelection			{ normalSelectionQ s }
	/ s:plainSelection			{ plainSelectionQ s }
;
normalSelection :: [ExpressionQ]
	= ex:expressionHs _:spaces '/' _:spaces sel:normalSelection
						{ ex : sel }
	/ ex:expressionHs			{ [ex] }
;
plainSelection :: [PlainExpressionQ]
	= ex:plainExpressionHs _:spaces '/' _:spaces sel:plainSelection
						{ ex : sel }
	/ ex:plainExpressionHs			{ [ex] }
;
expressionHs :: ExpressionQ
	= e:expression _:spaces '{' _:spaces h:hsExpLam _:spaces '}'
						{ expressionQ False (e, h) }
	/ e:expressionHsSugar			{ e }
;
expressionHsSugar :: ExpressionQ
	= '<' _:spaces h:hsExpLam _:spaces '>'	{ expressionSugar h }
;
plainExpressionHs :: PlainExpressionQ
	= rfs:(rf:plainHAReadFromLs _:spaces { rf })*
						{ plainExpressionQ rfs }
;
plainHAReadFromLs :: (Lookahead, ReadFromQ)
	= rf:plainReadFromLs			{ (Here, rf) }
	/ '&' rf:plainReadFromLs		{ (Ahead, rf) }
	/ '!' rf:plainReadFromLs		{ (NAhead "", rf) }
;
plainReadFromLs :: ReadFromQ
	= rf:readFromLs				{ rf }
	/ rf:selectCharsLs			{ rf }
;
expression :: [(Lookahead, CheckQ)]
	= l:nameLeaf_ _:spaces e:expression	{ l : e }
	/ 					{ [] }
;
nameLeaf_ :: (Lookahead, CheckQ)
	= '!' nl:nameLeafNoCom _:spaces com:comForErr?
						{ (NAhead $ maybe "" id com, nl) }
	/ '&' nl:nameLeaf			{ (Ahead, nl) }
	/ nl:nameLeaf				{ (Here, nl) }
;
nameLeaf :: CheckQ
	= n:pat1 _:spaces com:comForErr? ':' (rf, p):leaf
		{ check (n, maybe "" id com) rf p }
	/ n:pat1 _:spaces com:comForErr?
		{ check (n, maybe "" id com)
			(const $ return $ FromVariable Nothing) Nothing }
;
nameLeafNoCom :: CheckQ
	= n:pat1 _:spaces com:comForErr? ':' (rf, p):leaf
		{ check (n, maybe "" id com) rf p }
	/ n:pat1 _:spaces
		{ check (n, "") (const $ return $ FromVariable Nothing) Nothing }
;
comForErr :: String
	= '{' '-' '#' _:spaces '"' s:stringLit '"' _:spaces '#' '-' '}' _:spaces
						{ s }
;
leaf :: (ReadFromQ, Maybe (ExpQ, String))
	= rf:readFromLs t:test			{ (rf, Just t) }
	/ rf:readFromLs				{ (rf, Nothing) }
	/ t:test			{ (const $ return $ FromVariable Nothing,
						Just t) }
;
patOp :: PatQ
	= p:pat o:opConName po:patOp		{ uInfixP p o po }
	/ p:pat _:spaces '`' t:typ '`' _:spaces po:patOp
						{ uInfixP p (mkName t) po }
	/ p:pat					{ p }
;
pat :: PatQ
	= t:typ _:spaces ps:pats		{ conP (mkName t) ps }
	/ '(' o:opConName ')'_:spaces ps:pats	{ conP o ps }
	/ p:pat1				{ p }
;
pat1 :: PatQ
	= t:typ					{ conP (mkName t) [] }
	/ "_":variable				{ wildP }
	/ n:variable				{ varP $ mkName n }
	/ i:integer				{ litP (integerL i) }
	/ '-' _:spaces i:integer		{ litP (integerL $ - i) }
	/ '\'' c:charLit '\''			{ litP $ charL c }
	/ '"' s:stringLit '"'			{ litP $ stringL s }
	/ '(' p:patList ')'			{ tupP p }
	/ '[' p:patList ']'			{ listP p }
;
patList :: [PatQ]
	= p:patOp _:spaces ',' _:spaces ps:patList
						{ p : ps }
	/ p:patOp				{ [p] }
	/					{ [] }
;
opConName :: Name
	= ':' ot:opTail				{ mkName $ ':' : ot }
;
charLit :: Char
	= c:[c `notElem` "\\'"]			{ c }
	/ '\\' c:escapeC			{ c }
;
stringLit :: String
	= c:[c `notElem` "\"\\"] s:stringLit	{ c : s }
	/ '\\' c:escapeC s:stringLit		{ c : s }
	/					{ "" }
;
escapeC :: Char
	= '"'					{ '"' }
	/ '\''					{ '\'' }
	/ '\\'					{ '\\' }
	/ 'n'					{ '\n' }
	/ 't'					{ '\t' }
;
pats :: [PatQ]
	= p:pat _:spaces ps:pats		{ p : ps }
	/					{ [] }
;
readFromLs :: ReadFromQ
	= rf:readFrom '*'			{ (FromL List <$>) . rf }
	/ rf:readFrom '+'			{ (FromL List1 <$>) . rf }
	/ rf:readFrom '?'			{ (FromL Optional <$>) . rf }
	/ rf:readFrom				{ rf }
;
readFrom :: ReadFromQ
	= v:variable				{ const $ return $
							FromVariable $ Just v }
	/ '(' s:selection ')'			{ fromSelectionQ s }
	/ e:expressionHsSugar			{ fromSelectionQ $
							normalSelectionQ [e] }
;
selectCharsLs :: ReadFromQ
	= rf:selectChars '*'			{ (FromL List <$>) . rf }
	/ rf:selectChars '+'			{ (FromL List1 <$>) . rf }
	/ rf:selectChars '?'			{ (FromL Optional <$>) . rf }
	/ rf:selectChars			{ rf }
;
selectChars :: ReadFromQ
	= '[' cs:(c:[isLower c] { c })+ ']'	{ fromTokenChars cs }
	/ '[' cb:[cb `notElem` "\\-"] '-'
		ce:[ce `notElem` "\\-"] ']'	{ fromTokenChars [cb .. ce] }
	/ '\'' c:[c `notElem` "\\'"] '\''	{ fromTokenChars [c] }
;
test :: (ExpQ, String)
	= '[' h:hsExpLam _:spaces com:comForErr? ']'
						{ (h, maybe "" id com) }
;
hsExpLam :: ExpQ
	= '\\' _:spaces ps:pats _:spaces '-' '>' _:spaces e:hsExpTyp
						{ lamE ps e }
	/ e:hsExpTyp				{ e }
;
hsExpTyp :: ExpQ
	= eo:hsExpOp ':' ':' _:spaces t:hsTypeArr
						{ sigE eo t }
	/ eo:hsExpOp				{ eo }
;
hsExpOp :: ExpQ
	= l:hsExp _:spaces o:hsOp _:spaces r:hsExpOp
						{ uInfixE (l id) o r }
	/ e:hsExp				{ e id }
;
hsOp :: ExpQ
	= c:[c `elem` "+*/-!|&.^=<>$"] o:opTail { varE $ mkName $ c : o }
	/ ':' !':' o:opTail			{ conE $ mkName $ ':' : o }
	/ '`' v:variable '`'			{ varE $ mkName v }
	/ '`' t:typ '`'				{ conE $ mkName t }
;
opTail :: String
	= c:[c `elem` ":+*/-!|&.^=<>$"] s:opTail{ c : s }
	/					{ "" }
;
hsExp :: (ExpQ -> ExpQ) -> ExpQ
	= e:hsExp1 _:spaces h:hsExp		{ \f -> h (f e `appE`) }
	/ e:hsExp1				{ \f -> f e }
;
hsExp1 :: ExpQ
	= '(' l:(e:hsExpTyp {e})? _:spaces o:hsOp _:spaces r:(e:hsExpTyp {e})? ')'
						{ infixE l o r }
	/ '(' et:hsExpTpl ')'			{ tupE et }
	/ '[' et:hsExpTpl ']'			{ listE et }
	/ v:variable				{ varE $ mkName v }
	/ t:typ					{ conE $ mkName t }
	/ i:integer _:spaces			{ litE $ integerL i }
	/ '\'' c:charLit '\''			{ litE $ charL c }
	/ '"' s:stringLit '"'			{ litE $ stringL s }
	/ '-' _:spaces e:hsExp1			{ appE (varE $ mkName "negate") e }
;
hsExpTpl :: [ExpQ]
	= e:hsExpLam _:spaces ',' _:spaces et:hsExpTpl
						{ e : et }
	/ e:hsExpLam				{ [e] }
	/					{ [] }
;
hsTypeArr :: TypeQ
	= l:hsType '-' '>' _:spaces r:hsTypeArr
						{ appT (appT arrowT $ l id) r }
	/ t:hsType				{ t id }
;
hsType :: (TypeQ -> TypeQ) -> TypeQ
	= t:hsType1 ts:hsType			{ \f -> ts (f t `appT`) }
	/ t:hsType1				{ ($ t) }
;
hsType1 :: TypeQ
	= '[' ']' _:spaces			{ listT }
	/ '[' t:hsTypeArr ']' _:spaces		{ listT `appT` t }
	/ '(' _:spaces tt:hsTypeTpl ')' _:spaces
					{ foldl appT (tupleT $ length tt) tt }
	/ t:typToken				{ conT $ mkName t }
	/ '(' '-' '>' ')' _:spaces		{ arrowT }
;
hsTypeTpl :: [TypeQ]
	= t:hsTypeArr ',' _:spaces tt:hsTypeTpl
						{ t : tt }
	/ t:hsTypeArr				{ [t] }
	/					{ [] }
;
typ :: String
	= u:upper t:tvtail			{ u : t }
;
variable :: String
	= l:lower t:tvtail			{ l : t }
;
tvtail :: String
	= a:alpha t:tvtail			{ a : t }
	/ 					{ "" }
;
integer :: Integer
	= dh:digit ds:(d:digit { d })*		{ read $ dh : ds }
;
alpha :: Char
	= u:upper				{ u }
	/ l:lower				{ l }
	/ d:digit				{ d }
	/ '\''					{ '\'' }
;
upper :: Char
	= u:[isUpper u]				{ u }
;
lower :: Char
	= l:[isLower l || l == '_' ]		{ l }
;
digit :: Char
	= d:[isDigit d]				{ d }
;
spaces :: ()
	= _:space _:spaces			{ () }
	/ 					{ () }
;
space :: ()
	= s:[isSpace s]				{ () }
	/ '-' '-' _:notNLString _:newLine	{ () }
	/ _:comment				{ () }
;
notNLString :: String
	= !_:newLine c s:notNLString		{ c : s }
	/					{ "" }
;
newLine :: ()
	= '\n'					{ () }
;
comment :: ()
	= '{' '-' !'#' _:comments _:comEnd	{ () }
;
comments :: ()
	= _:notComStr _:comment _:comments	{ () }
	/ _:notComStr				{ () }
;
notComStr :: ()
	= !_:comment !_:comEnd _ _:notComStr	{ () }
	/					{ () }
;
comEnd :: ()
	= '-' '}'				{ () }
;

|]
